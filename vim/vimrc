" Don't care about vi support
set nocompatible

" Add all plugins from pathogen
call pathogen#helptags()
call pathogen#runtime_append_all_bundles() 

" BEGIN INDENTATION
filetype plugin indent on " Use different indentation based on filetype
set expandtab " Turn <TAB> into spaces.
set softtabstop=4 " Number of spaces <TAB> expands to
set shiftwidth=4 " The width when pressing >>,<< or == and ai
set autoindent " Indent a new line equal to the one above.

" BEGIN LOOK
set t_Co=256 " Enable 256 colors in terminals that support it
syntax on " Use syntax highlightning
set number " Show line numbers to the left
set background=dark " Optimize for dark background
set ruler " Show line and column separated by a comma in lower right
set showcmd " Show linecount in visual mode
let g:zenburn_high_Contrast = 1 " Use zenburn in darker mode
colorscheme zenburn " Use ir_black as default colorscheme

" Turns on highlightning of characters which are after column 80
function EnableOverLength()
    highlight overlength ctermbg=red ctermfg=white guibg=#b0041b
    match OverLength /\%81v.\+/ 
    if v:version >= 703
        set colorcolumn=80
        hi ColorColumn guibg=#2d2d2d
    endif
    let g:overlength_on = 1
endfunction

" Turns off highlightning of characters which are after column 80
function DisableOverLength()
        highlight clear OverLength
        if v:version >= 703
            highlight clear ColorColumn
        endif
        let g:overlength_on = 0
endfunction

" Toggles highlightning of characters which are after column 80
function ToggleOverLength()
    if exists("g:overlength_on") && g:overlength_on == 1
        call DisableOverLength()
    else
        call EnableOverLength()
    endif
endfunction

" Enable the highlightning for all new buffers
function MaybeEnableOverLength()
    if !exists("g:overlength_on") || g:overlength_on == 1
        call EnableOverLength()
    endif
endfunction
autocmd BufEnter * call MaybeEnableOverLength() 

" BEGIN SYNTAX FILES
au BufRead,BufNewFile *.json set filetype=json
au BufRead,BufNewFile wscript set filetype=python

" BEGIN SEARCH
set hlsearch  " Highlight search
set incsearch  " Incremental search, search as you type
set smartcase " Ignore case when searching lowercase

" BEGIN COMPATABILITY
set backspace=2 " Fix backspace for strange terminals
autocmd BufRead,BufNewFile *.tex set wrapmargin=1 " Wrap lines in latex docs
" Saves additional stuff when saving a session
set sessionoptions=blank,buffers,curdir,folds,help,resize,tabpages,winsize

" BEGIN KEYBOARD MAPPINGS

" Table for remembering different mapping modes:
"    map type     | normal | visual+select | operator-pending
" noremap (map)   |   y    |       y       |         y
" nnoremap (nmap) |   y    |       -       |         -
" vnoremap (vmap) |   -    |       y       |         -
" onoremap (omap) |   -    |       -       |         y

" Change leader
let mapleader=","

" Open and close NERDTree
nmap <leader>t :NERDTreeToggle<CR>

" <CTRL>-t to create a new tab (in both insert and normal mode)
map <C-t> :tabnew<CR>
imap <C-t> <ESC>:tabnew<CR>

" Map <C-h> and <C-l> to switch tabs
map <C-h> :tabprevious<CR>
map <C-l> :tabnext<CR>
imap <C-h> <Esc>:tabprevious<CR>i
imap <C-l> <Esc>:tabnext<CR>i

" Map <C-j> to break the line at cursor
nmap <NL> i<CR><RIGHT><ESC>

" <SPACE> to switch windows (in normal mode)
map <Space> <c-w>w

" ,= to even out all splits
nmap <leader>= <C-w>=

" ,w to open new vertical split and move to it
nmap <leader>w <C-w>v<C-w>l

" <DASH> to turn on/off highlighted search
nnoremap <silent><leader><space> :noh<cr>

" Easy copy/paste to clipboard
map <leader>y "+y
nmap <leader>p "+p 

" Remap ESC to ii
imap jj <Esc>

" Toogle highlight with ,h
map <silent><leader>h :call ToggleOverLength()<cr>
